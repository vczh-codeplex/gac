include:"..\..\..\Source\Parsing\Parsing.h"
namespace:vczh.whoknows.xmlutility
classPrefix:Xml
guard:VCZH_PARSING_XML
parser:ParseDocument(XDocument)
parser:ParseElement(XElement)
grammar:

class Node ambiguous(AmbiguousNode)
{
}

class AmbiguousNode : Node
{
	Node[] items;
}

class Text : Node
{
	token content (XmlUnescapeTextFragment);
}

class CData : Node
{
	token content (XmlUnescapeCData);
}

class Attribute : Node
{
	token name;
	token value (XmlUnescapeAttributeValue);
}

class Comment : Node
{
	token content (XmlUnescapeComment);
}

class Element : Node
{
	token name;
	token closingName;
	Attribute[] attributes;
	Node[] subNodes (XmlMergeTextFragment);
}

class Instruction : Node
{
	token name;
	Attribute[] attributes;
}

class Document : Node
{
	Instruction[] instructions;
	Comment[] comments;
	Element rootElement;
}

enum DirectEnum
{
	A,B,C,
}

class FirstClass
{
	enum IndirectEnum
	{
		D,E,F,
	}
	
	class SecondClass
	{
		class ThirdClass
		{
			token field1;
		}
	}

	class SecondClassChild : SecondClass
	{
		token field3;
	}

	token field4;
}

class FirstClassChild : FirstClass
{
	token field2;
}

class ThirdClassChild : FirstClass.SecondClass.ThirdClass
{
	token field5;
}

token INSTRUCTION_OPEN = "/</?";
token INSTRUCTION_CLOSE = "/?/>";
token COMPLEX_ELEMENT_OPEN = "/<//";
token SINGLE_ELEMENT_CLOSE = "///>";
token ELEMENT_OPEN = "/<";
token ELEMENT_CLOSE = "/>";
token EQUAL = "/=";

token NAME = "[a-zA-Z0-9:_/-]+";
token ATTVALUE = """([^""&]|&/l+;)*""";
token COMMENT = "/</!--[^>]*/>";
token CDATA = "/</!/[CDATA/[([^/]]|/][^/]]|/]/][^>])*/]/]/>";
token TEXT = "([^<>""& /r/n/ta-zA-Z0-9:_/-]|&/l+;)+";

discardtoken SPACE = "/s+";

rule Attribute XAttribute = NAME:name "=" ATTVALUE:value as Attribute;
rule Text XText = (NAME:content | EQUAL:content | TEXT:content) as Text;
rule CData XCData = CDATA:content as CData;
rule Comment XComment = COMMENT:content as Comment;
rule Element XElement = "<" NAME:name {XAttribute:attributes} ("/>" | ">" {XSubNode:subNodes} "</" NAME:closingName ">") as Element;
rule Node XSubNode = !XText | !XCData | !XComment | !XElement;
rule Instruction XInstruction = "<?" NAME:name {XAttribute:attributes} "?>" as Instruction;
rule Document XDocument = {XInstruction:instructions | XComment:comments} XElement:rootElement as Document;